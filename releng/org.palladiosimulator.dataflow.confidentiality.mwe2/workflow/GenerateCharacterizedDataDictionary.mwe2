module org.palladiosimulator.dataflow.dictionary.characterized.behavior.dsl.GenerateBehaviorDefinition

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import tools.mdsd.ecoreworkflow.mwe2lib.bean.EclipseRCPSupportingStandaloneSetup

var workspaceRoot = "../.."
var rootPath = "${workspaceRoot}/bundles"
var dataDictionaryProjectName = "org.palladiosimulator.dataflow.dictionary.characterized"
var dataFlowDiagramProjectName = "org.palladiosimulator.dataflow.diagram.characterized"
var baseName = "org.palladiosimulator.dataflow.dictionary.characterized.dsl"

Workflow {
	
	bean = EclipseRCPSupportingStandaloneSetup {
		scanClassPath = true
		platformUri = workspaceRoot
		
		uriMap = {
			from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.ecore"
			to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.ecore"
		}
		
		uriMap = {
			from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
			to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
		}
		
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.dataflow.diagram/model/DataFlowDiagram.genmodel"
			to = "platform:/resource/org.palladiosimulator.dataflow.diagram/model/DataFlowDiagram.genmodel"
		}
		
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.dataflow.dictionary/model/DataDictionary.genmodel"
			to = "platform:/resource/org.palladiosimulator.dataflow.dictionary/model/DataDictionary.genmodel"
		}
		
		uriMap = {
			from = "platform:/plugin/${dataDictionaryProjectName}/model/DataDictionaryCharacterized.genmodel"
			to = "platform:/resource/${dataDictionaryProjectName}/model/DataDictionaryCharacterized.genmodel"
		}
		
		uriMap = {
			from = "platform:/plugin/${dataFlowDiagramProjectName}/model/DataFlowDiagramCharacterized.genmodel"
			to = "platform:/resource/${dataFlowDiagramProjectName}/model/DataFlowDiagramCharacterized.genmodel"
		}
		
		registerGenModelFile = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
	}
	
	component = XtextGenerator {
		cleaner = XtextDirectoryCleaner {
			exclude = ".gitkeep"
		}
		configuration = {
			project = StandardProjectConfig {
				baseName = "${baseName}"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					root = "${workspaceRoot}/tests/${baseName}.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
					root = "${workspaceRoot}/tests/${baseName}.ui.tests"
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "${baseName}.CharacterizedDataDictionary"
			fileExtensions = "ddc"
			referencedResource = "platform:/resource/org.palladiosimulator.dataflow.dictionary.characterized/model/DataDictionaryCharacterized.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = false
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}

